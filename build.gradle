plugins {
    id "java"
    id 'com.github.johnrengelman.shadow' version '7+'
}

group = "moe.plushie.armourers_workshop.plugin"
archivesBaseName = "armourersworkshop-plugin"

if (version == "unspecified") {
    version = "2.0.0-homebaked"
}

repositories {
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/groups/public/" }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    maven { url "https://jitpack.io/" }
    maven { url "https://repo.codemc.org/repository/maven-public/" }
}

dependencies {
    compileOnly "org.jetbrains:annotations:23.0.0"

    compileOnly "org.spigotmc:spigot-api:1.16.5-R0.1+"

    compileOnly "io.netty:netty-buffer:4.1.80+"
    compileOnly "io.netty:netty-handler:4.1.80+"
    compileOnly "com.comphenix.protocol:ProtocolLib:4.8.0+"

    implementation "commons-io:commons-io:2.11.0"
    implementation "commons-lang:commons-lang:2.6"
    implementation "com.github.Querz:NBT:6.1+"
    implementation "dev.jorel:commandapi-shade:8.8.0"
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

tasks.register("prepareRun") {
    dependsOn shadowJar
    doLast {
        shadowJar.outputs.files.each {
            ant.copy(file: it, todir: "${buildDir}/../run/plugins", overwrite: true)
        }
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }
}

shadowJar {
    archiveBaseName.set(project.getArchivesBaseName())
    archiveClassifier.set("")
    archiveVersion.set(project.getVersion())

    exclude "META-INF/**"

    def pkg = "moe.plushie.armourers_workshop.plugin.lib"
    relocate("org.apache", pkg)
    relocate("dev.jorel", pkg)
    relocate("net.querz", pkg)
}


assemble.dependsOn shadowJar